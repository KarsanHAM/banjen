apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'spoon'
apply from: '../jacoco.gradle'
apply plugin: 'com.nimbledroid.profiler'

def appId = "com.makingiants.android.banjotuner"
def appVersionName = "1.4.6"
def appVersionCode = 22

android {
  compileSdkVersion setup.targetSdk
  buildToolsVersion setup.buildTools

  defaultConfig {
    applicationId appId
    minSdkVersion setup.minSdk
    targetSdkVersion setup.targetSdk
    versionCode appVersionCode
    versionName appVersionName

    // Enable just English configs for dependencies, allowing to shrink apk size more
    resConfigs "en", "pt"

    resValue "string", "ads_unit_id_banner", "$BANJEN_ADS_UNIT_ID_BANNER"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      minifyEnabled false
      useProguard false // For performance
      testCoverageEnabled = true

      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      useProguard true

      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  aaptOptions {
    // Disable automatic automatic image optimization (do it manually)
    cruncherEnabled = false
  }
}

dependencies {
  compile "com.android.support:appcompat-v7:${deps.supportLib}"
  compile "com.google.android.gms:play-services-ads:${deps.playServices}"

  compile "org.jetbrains.kotlin:kotlin-stdlib:$deps.kotlin"

  compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') { transitive = true; }

  testCompile 'junit:junit:4.12'
  testCompile "org.jetbrains.kotlin:kotlin-stdlib:$deps.kotlin"
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$deps.kotlin"

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  androidTestCompile('com.jraska:falcon-spoon-compat:1.0.4') {
    exclude group: 'com.squareup.spoon', module: 'spoon-client'
  }
  androidTestCompile 'com.squareup.spoon:spoon-client:1.7.1'

  androidTestCompile('com.android.support.test:runner:0.5') {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
  androidTestCompile('com.android.support.test:rules:0.5') {
    exclude group: 'com.android.support', module: 'support-annotations'
  }
}

//jacoco {
//  toolVersion = "0.7.4.201502262128"
//}

spoon {
  debug = true
  codeCoverage = true
  // To grant permissions to Android M >= devices */
  grantAllPermissions = true
}

nimbledroid {
  apiKey BANJEN_NIMBLEDROID_API_KEY
  apkFilename 'app/build/outputs/apk/app-debug.apk'
}
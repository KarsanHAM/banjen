buildscript {
  repositories {
    jcenter() // gradle-versions-plugin
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.21.7'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.13.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$deps.kotlin"
    classpath "org.jetbrains.kotlin:kotlin-android-extensions:$deps.kotlin"
  }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.github.ben-manes.versions'

def appId = "com.makingiants.android.banjotuner"
def appVersionName = "1.4.1"
def appVersionCode = 16

android {
  compileSdkVersion setup.targetSdk
  buildToolsVersion setup.buildTools

  signingConfigs {
    release {
      storeFile file(BANJEN_SIGN_PATH)
      storePassword BANJEN_SIGN_PWD
      keyAlias BANJEN_SIGN_ALIAS
      keyPassword BANJEN_SIGN_PWD
    }
  }

  defaultConfig {
    applicationId appId
    minSdkVersion setup.minSdk
    targetSdkVersion setup.targetSdk
    versionCode appVersionCode
    versionName appVersionName

    // Enable just English configs for dependencies, allowing to shrink apk size more
    resConfigs "en", "pt"

    // Add version and build number to apk name
    archivesBaseName = "banjen-${appVersionName}-${appVersionCode}"

    resValue "string", "ads_unit_id_banner", "$BANJEN_ADS_UNIT_ID_BANNER"
  }

  buildTypes {
    debug {
      minifyEnabled false
      useProguard false // For performance

      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      useProguard true

      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  aaptOptions {
    // Disable automatic automatic image optimization (do it manually)
    cruncherEnabled = false
  }
}

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

dependencies {
  compile "com.android.support:appcompat-v7:${deps.supportLib}"
  compile "com.android.support:support-annotations:${deps.supportLib}"
  compile "com.google.android.gms:play-services-ads:${deps.playServices}"

  compile "org.jetbrains.kotlin:kotlin-stdlib:$deps.kotlin"

  compile('com.crashlytics.sdk.android:crashlytics:2.6.4@aar') { transitive = true; }
}